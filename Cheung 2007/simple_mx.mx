#NGROUPS 1			! One group analysis
Mx: Constructing CIs on simple mediating effect 
DATA NInput=3			! No. of variables
RECTANGULAR FILE = simple.dat	! Raw data are used
LABELS X M Y

! The model being analyzed: X -> M -> Y
 Begin Matrices;
  S Symm 3 3
  A Full 3 3
  M Full 1 3		! Means
  F Iden 3 3		! Selection matrix
  I Iden 3 3		! Identity matrix
  T Full 1 4		! Used to select elements for constraint
  U Full 1 4		! Used to select elements for constraint
 End Matrices;

 Matrix S		! Starting values
  1
  0 1
  0 0 1
 Specify S		! Specify fixed and free parameters for variances and
  1 			! error variances
  0 2 			! 0: fixed parameters; others: free parameters
  0 0 3 
 Label Row S
  X M Y
 Label Col S
  X M Y

 Matrix A		! Starting values
  0 0 0 
  0.5 0 0 
  0 0.5 0
 Specify A		! Specify fixed and free parameters for path
  0 0 0 		! coefficients
  5 0 0 
  6 7 0 
 Label Row A
  X M Y
 Label Col A
  X M Y

 Matrix M		! Starting values
 0
 0
 0
 Specify M		! Specify free parameters for the means
 10
 11 
 12

! Elements for calculating indirect effect
Matrix T 2 1 2 1		! X -> M, A(2,1)
Matrix U 3 2 3 2		! M -> Y, A(3,2)

Begin Algebra;
	J = \part(A, T);	! Select the required element
	K = \part(A, U);	! Select the required element
	Z = J*K;		! Z: Indirect effect with one variable
End Algebra;

 Means M;
 Covariance  F & ((I - A)~ & S);
 Interval Z 1 1		 ! Confidence interval on the indirect effect
END
