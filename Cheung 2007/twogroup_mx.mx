#NGROUPS 3	! Two independent groups and one more group for contraints 
Mx: Constructing CIs on the difference of the mediating effects between two independent groups
DATA NInput=4
RECTANGULAR FILE= twogroup.dat
LABELS X M Y Group	! Group is the grouping variable
SELECT IF Group = 1;	! Group=1: select data for group 1
SELECT X M Y;		! Use these 3 variables in the analysis only

! The model being analyzed: X -> M -> Y
 Begin Matrices;
  S Symm 3 3
  A Full 3 3
  M Full 1 3		! Means
  F Iden 3 3		! Selection matrix
  I Iden 3 3		! Identity matrix
  T Full 1 4		! Used to select elements for constraint
  U Full 1 4		! Used to select elements for constraint
 End Matrices;

 Matrix S
  1
  0 1
  0 0 1
 Specify S
  1 
  0 2 
  0 0 3 
 Label Row S
  X M Y
 Label Col S
  X M Y

 Matrix A
  0 0 0 
  0.5 0 0 
  0.0 0.5 0
 Specify A
  0 0 0 
  4 0 0 
  5 6 0 
 Label Row A
  X M Y
 Label Col A
  X M Y

 Matrix M
 0
 0
 0
 Specify M
 7
 8 
 9

! Elements for calculating indirect effect
Matrix T 2 1 2 1		! X -> M, A(2,1)
Matrix U 3 2 3 2		! M -> Y, A(3,2)

Begin Algebra;
	J = \part(A, T);	! Select the required element
	K = \part(A, U);	! Select the required element
End Algebra;

 Means M;
 Covariance  F & ((I - A)~ & S);
END GROUP;

G2: Group=2			! Model for group 2
DATA NInput=4
RECTANGULAR FILE= twogroup.dat
LABELS X M Y Group 
SELECT IF Group = 2;	! Group=2: select data for group 2
SELECT X M Y;		! Use these 3 variables in the analysis only

 Begin Matrices;
  S Symm 3 3
  A Full 3 3
  M Full 1 3		! Means
  F Iden 3 3		! Selection matrix
  I Iden 3 3
  T Full 1 4
  U Full 1 4
 End Matrices;

 Matrix S
  1
  0 1
  0 0 1
 Specify S
  11 
  0 12 
  0 0 13 
 Label Row S
  X M Y
 Label Col S
  X M Y

 Matrix A
  0 0 0 
  0.5 0 0 
  0 0.5 0
 Specify A
  0 0 0 
  14 0 0 
  15 16 0 
 Label Row A
  X M Y
 Label Col A
  X M Y

 Matrix M
 0
 0
 0
 Specify M
 17
 18 
 19

! Elements for calculating indirect effect
Matrix T 2 1 2 1		! X -> M, A(2,1)
Matrix U 3 2 3 2		! M -> Y, A(3,2)

Begin Algebra;
	J = \part(A, T);	! Select the required element
	K = \part(A, U);	! Select the required element
End Algebra;

 Means M;
 Covariance  F & ((I - A)~ & S);
END GROUP;

G3: Calculation for the difference on the indirect effects between two groups
Calculation
Begin Matrices;
 N Full 1 1 = J1		! Get J from group 1 and put it into N
 P Full 1 1 = K1		! Get K from group 1 and put it into P
 R Full 1 1 = J2		! Get J from group 2 and put it into R
 T Full 1 1 = K2		! Get K from group 2 and put it into T
End Matrices;

Begin Algebra;
	Z = N*P - R*T;		! Z: Difference on the indirect effects in two 
				! independent groups
End Algebra;

	Interval Z 1 1;		! CI on the difference on Z
End Group;


